import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
  id 'java'
  id 'org.openapi.generator'
  id 'maven-publish'
}

group = "com.wci.automap"
version = "2.2.0.202511"

// Input Specification locations. When using the inputFile, make sure you've updated
// the doc with the changes you want to generate
def inputURL = 'https://automap.terminology.tools/v3/api-docs'

repositories {
  mavenCentral()
}

sourceCompatibility = 17
targetCompatibility = 17

test {
  useJUnitPlatform()
}

final artifactGroup = "${group}"
final artifactVersion = "${version}"
final artifactId = 'wci-automap-java-client'

/**
 * Builds a Java client by default: https://openapi-generator.tech/docs/generators/java/
 * To use a local file, replace {remoteInputSpec.set(inputURL)} with input {inputSpec.set(inputFile)}
*/
tasks.register('buildJavaSdk', GenerateTask) {
    generatorName.set("java")
    remoteInputSpec.set(inputURL)
    outputDir.set(layout.buildDirectory.dir("java").get().getAsFile().getAbsolutePath())
    apiPackage.set("api")
    invokerPackage.set("com.wci.automap.client.invoker")
    modelPackage.set("com.wci.automap.client.model")
    configOptions.set([
        "useJakartaEe": "true",
        "library": "jersey3",
        "serializationLibrary": "jackson",
        "hideGenerationTimestamp": "true",
        "java8": "false",
        "dateLibrary": "legacy"
    ])
}

/**
 * Cannot exclude from build, so remove the unwanted APIs and associated tests.
 */
tasks.register('removeUnwantedApis') {
    doLast {
        delete fileTree(
            dir: "${buildDir}/java/src/main/java/com/wci/automap/api",
            include: ['NlmApi.java', 'TerminologyApi.java']
        )
        delete fileTree(
            dir: "${buildDir}/java/src/test/java/com/wci/automap/api",
            include: ['NlmApiTest.java', 'TerminologyApiTest.java']
        )
    }
}

/**
 * Patch the generated build.gradle file to use Java 17 and the correct artifactId.
 * OpenAPI Generator 7.5.0 uses Java 8 by default, so we need to patch the build.gradle file to use Java 17.
 */
tasks.register('patchGeneratedJavaVersion') {
    doLast {
        def gradleFile = file("$buildDir/java/build.gradle")
        if (gradleFile.exists()) {
            println "Patching Java version to 17 in ${gradleFile}..."
            def text = gradleFile.text
            text = text.replaceAll(
                /sourceCompatibility\s*=\s*JavaVersion\.VERSION_1_8/,
                'sourceCompatibility = JavaVersion.VERSION_17'
            ).replaceAll(
                /targetCompatibility\s*=\s*JavaVersion\.VERSION_1_8/,
                'targetCompatibility = JavaVersion.VERSION_17'
            ).replaceAll(
                /sourceCompatibility\s+JavaVersion\.VERSION_1_8/,  // Android syntax
                'sourceCompatibility JavaVersion.VERSION_17'
            ).replaceAll(
                /targetCompatibility\s+JavaVersion\.VERSION_1_8/,  // Android syntax
                'targetCompatibility JavaVersion.VERSION_17'
            ).replaceAll(
                /sourceCompatibility\s*=\s*['"]1\.8['"]/,
                'sourceCompatibility = \'17\''
            ).replaceAll(
                /targetCompatibility\s*=\s*['"]1\.8['"]/,
                'targetCompatibility = \'17\''
            )
            gradleFile.text = text
        } else {
            println "No generated build.gradle found at ${gradleFile}"
        }
    }
}

/**
 * Patch the generated build.gradle file to use the correct group, version, artifactId, and JAR name.
 */
tasks.register('patchGeneratedArtifactId') {
    doLast {
        def gradleFile = file("$buildDir/java/build.gradle")
        if (gradleFile.exists()) {
            println "Patching group, version, artifactId, and JAR name in ${gradleFile}..."
            def text = gradleFile.text
            text = text.replaceFirst(
                /group\s*=\s*['"].*['"]/,
                "group = '${artifactGroup}'"
            )
            text = text.replaceFirst(
                /version\s*=\s*['"].*['"]/,
                "version = '${artifactVersion}'"
            )
            text = text.replaceAll(
                /artifactId\s*=\s*['"][^'"]*['"]/,
                "artifactId = '${artifactId}'"
            )
            // Patch JAR file name
            if (text.contains('tasks.withType(Jar)')) {
                text = text.replaceAll(
                    /archiveBaseName\.set\(['"].*['"]\)/,
                    "archiveBaseName.set('${artifactId}')"
                ).replaceAll(
                    /archiveVersion\.set\(['"].*['"]\)/,
                    "archiveVersion.set('${artifactVersion}')"
                )
            } else {
                text += """
tasks.withType(Jar) {
    archiveBaseName.set('${artifactId}')
    archiveVersion.set('${artifactVersion}')
}
"""
            }
            gradleFile.text = text
        } else {
            println "No generated build.gradle found at ${gradleFile}"
        }
    }
}

// Task to generate Maven metadata
tasks.register("generateMavenMetadata") {
    doLast {
        def groupPath = project.group.toString().replace('.', '/')
        def metaInfDir = file("$buildDir/maven-metadata/META-INF/maven/${groupPath}/${artifactId}")
        metaInfDir.mkdirs()

        // pom.properties
        file("$metaInfDir/pom.properties").text = """\
# Generated by Gradle
groupId=${project.group}
artifactId=${artifactId}
version=${project.version}
"""

        // pom.xml
        file("$metaInfDir/pom.xml").text = """\
<?xml version="1.0" encoding="UTF-8"?>
<project xmlns="http://maven.apache.org/POM/4.0.0"
         xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
         xsi:schemaLocation="http://maven.apache.org/POM/4.0.0 http://maven.apache.org/maven-v4_0_0.xsd">
    <modelVersion>4.0.0</modelVersion>
    <groupId>${project.group}</groupId>
    <artifactId>${artifactId}</artifactId>
    <version>${project.version}</version>
    <packaging>jar</packaging>
</project>
"""
    }
}

tasks.register("makeGeneratedJavaSdkJar", Jar) {
    dependsOn('buildJavaSdk', 'generateMavenMetadata')

    from("$buildDir/java/src/main/java")
    from("$buildDir/maven-metadata") {
        into ""  // Include META-INF at root
    }

    archiveBaseName.set(artifactId)
    archiveVersion.set(version)
    destinationDirectory = layout.buildDirectory.dir("libs").get().asFile

    manifest {
        attributes(
            'Implementation-Title': artifactId ?: 'unknown',
            'Implementation-Version': version ?: 'unspecified',
            'Implementation-Vendor-Id': project.group.toString(),
            'Built-By': System.getProperty('user.name')
        )
    }
}


/**
 * Run the tasks in the correct order.
 */
tasks.named('buildJavaSdk') {
    finalizedBy('removeUnwantedApis', 'patchGeneratedJavaVersion', 'patchGeneratedArtifactId', 'makeGeneratedJavaSdkJar')
}

/** Runs all code-gen tasks for each language to generate the clients */
tasks.register("buildClients") {
  dependsOn("buildJavaSdk")
  doLast {
    println 'All client sdk generated. They can be found in the code-gen/build folder.'
  }
}

compileJava.dependsOn tasks.named("buildClients")
build.dependsOn("buildClients")
publishToMavenLocal.dependsOn = ["buildJavaSdk"]
